Option Explicit
'********DO NOT remove or change the expand line or the included file contents******************
'  MachStdMIll license terms REQUIRE that the copyright and License terms remain a part of this source file
#expand <Masters\Headers\CopyRightAndLicenseNotice>	
'**************************************************************************************
#expand <Masters\Headers\MachConstants>

	' GOTO WC Zero button

	Const GotoZs = 1
	Const StraightXYZPathThenA = 2
	Const XYThenZThenA = 3
	Const ZThenXYThenA = 4
	
	
	Const OKButton = 1
	
	Dim RestoreIncrMode As Boolean
	Dim MoveMode As Integer

	' ****************************************
	' the line below is the line to change to set the default movement sequence for GotoZero
	' by default this script is set to use the same motion that the Mach API call GotoZs (v3 OEM code 1017) does.
	MoveMode = GotoZs
	' ****************************************
	
	if GetOEMLED(ToolOffsetONOEMLED) = 0 then 
		' TLOs are off! 
		' Ask if the user really wants to go to WC 0 without mach compensating for TLO.
		If (MsgBox("Rapid move to WC origin with Tool Length Offsets off?", 1+32+256, "Please Confirm:") ) = OKButton then
			' yep, he wants the move
			call RapidToWCOrigin()
		Else
			' operator canceled (or some other return value), don't change position
			' just fall thru to end of button code.
		End if
	else
		' TLO is on 
		call RapidToWCOrigin()
	end if
	
	exit sub
	
sub RapidToWCOrigin()
	' find G90 mode to restore in case we change it
	If GetOEMLED(MachAbsoluteModeOEMLED) Then
	  ' we are in ABS mode 
	  RestoreIncrMode = False
	Else
	  ' we are in incr mode
	  RestoreIncrMode = True
	End If

	Code("G90")

	Select Case MoveMode
		' note: no explicit movement waits are used in this script as this is an MSM script and MSM only runs on versions of mach which have the 
		' semantics that the code call does not return until afte the movement is complete.
	Case GotoZs:
		DoOEMButton(MachGotoZsOEMBtn)
		
	Case StraightXYZPathThenA:
		Code("G00 X0 Y0 Z0")	' no cDiam(x)  needed as the x value is 0 
		Code("G00 A0")
		
	Case XYThenZThenA:
		Code("G00 X0 Y0")	' no cDiam(x)  needed as the x value is 0 
		Code("G00 Z0")
		Code("G00 A0")
		
	Case ZThenXYThenA:
		Code("G00 Z0")
		Code("G00 X0 Y0")	' no cDiam(x)  needed as the x value is 0 
		Code("G00 A0")
	Case Else:
		MsgBox "MSM: ERROR: invalid movement choice in GotoZ button script",0,"GotoWCOrigin"
	End Select
	
	If RestoreIncrMode Then
		Code("G91")
	End If

	' the script exits leaving us in G00 mode - no way to test and restore G00/G01/G02/G03 in v3
	Exit Sub
end sub	' RapidToWCOrigin
